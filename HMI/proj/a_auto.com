;**************MASK21:内/外/磨削中心自动对刀:panel_21********************
//M(Mask21/$85026//)

	DEF SIGNAL=(I/*0=$85013,1=$85014//$85534,$85534,,/WR4//"/Plc/DB2700.DBX1.0"/10,10,40/50,10,50/"#00FF00");测头信号
	DEF SIGNAL_1=(I/*0=$85013,1=$85014//$85534,$85534,,/WR4//"/Plc/DB10.DBX107.0"/10,10,40/50,10,50/"#00FF00");测头信号1
	DEF SIGNAL_2=(I/*0=$85013,1=$85014//$85534,$85534,,/WR4//"/Plc/DB10.DBX107.1"/0,0,0/110,10,50/"#00FF00");测头信号2
	DEF VAR7=(I///$85577,$85577,,/WR1//"/NC/_N_NC_GD2_ACX/LADAO[28]"/10,30,110/80,30,60//);自动测量当前测量齿槽

	DEF X_IM=(R///$85118,$85118,,/WR1//"$AA_IM[X]"/10,60,20/30,60,60/3);X_IM
	DEF Y_IM=(R///$85126,$85126,,/WR1//"$AA_IM[Y]"/10,80,20/30,80,60/3);Y_IM
	DEF Z_IM=(R///$85119,$85119,,/WR1//"$AA_IM[Z]"/10,100,20/30,100,60/3);Z_IM
	DEF A_IM=(R///$85122,$85122,,/WR1//"$AA_IM[A]"/120,60,20/140,60,60/3);A_IM
	DEF B_IM=(R///$85121,$85121,,/WR1//"$AA_IM[B]"/120,80,20/140,80,60/3);B_IM
	DEF C_IM=(R///$85120,$85120,,/WR4//"$AA_IM[C]"/120,100,20/140,100,60/3);C_IM

	;ifIsCeTouCeLiangBegin
	DEF KAIGUAN=(I//0//WR4//"/NC/_N_NC_GD2_ACX/TOOL_SET[32]"/0,0,0/0,0,0);自动对刀是否有接近开关测量机构
	DEF CETOU=(I//1//WR4//"/NC/_N_NC_GD2_ACX/TOOL_SET[50]"/0,0,0/0,0,0);自动对刀是否有测量头测量结构
	DEF CETOU_1=(I//0//WR4//"/NC/_N_NC_GD2_ACX/TOOL_SET[53]"/0,0,0/0,0,0);测量头(0-测量机构1/1-测量机构2)
	;ifIsCeTouCeLiangEnd

	DEF VAR0=(R///$85570,$85570,,/WR2//"/NC/_N_NC_GD2_ACX/LADAO[21]"/320,40,150/440,40,60/,"#d3e8ff"/);自动测量起始X位置
	DEF POS_INPUT_1=(I/*0=$85017,1=$85017/0/$85058,$85058,,/WR2///0,0,0/530,40,15/);坐标写入键
	DEF VAR5=(R///$85575,$85575,,/WR2//"/NC/_N_NC_GD2_ACX/LADAO[26]"/320,60,150/440,60,60/,"#d3e8ff"/);自动测量Y轴前刃测量位
	DEF POS_INPUT_6=(I/*0=$85017,1=$85017/0/$85058,$85058,,/WR2///0,0,0/530,60,15/);坐标写入键
	DEF VAR2=(R///$85572,$85572,,/WR2//"/NC/_N_NC_GD2_ACX/LADAO[23]"/320,80,150/440,80,60/,"#d3e8ff"/);自动测量起始Z位置
	DEF POS_INPUT_3=(I/*0=$85017,1=$85017/0/$85058,$85058,,/WR2///0,0,0/530,80,15/);坐标写入键
	DEF VAR3=(R///$85573,$85573,,/WR2//"/NC/_N_NC_GD2_ACX/LADAO[24]"/320,100,150/440,100,60/,"#d3e8ff"/);自动测量起始A位置
	DEF POS_INPUT_4=(I/*0=$85017,1=$85017/0/$85058,$85058,,/WR2///0,0,0/530,100,15/);坐标写入键
	DEF VAR4=(R///$85574,$85574,,/WR2//"/NC/_N_NC_GD2_ACX/LADAO[25]"/320,120,150/440,120,60/,"#d3e8ff"/);自动测量起始B位置
	DEF POS_INPUT_5=(I/*0=$85017,1=$85017/0/$85058,$85058,,/WR2///0,0,0/530,120,15/);坐标写入键
	DEF VAR_C=(R///$85500,$85500,,/WR4//"/NC/_N_NC_GD2_ACX/INI[88]"/320,140,150/440,140,60/,"#d3e8ff"/);自动测量起始C位置
	DEF POS_INPUT_7=(I/*0=$85017,1=$85017/0/$85058,$85058,,/WR4///0,0,0/530,140,15/);坐标写入键

	DEF VAR6=(R/0,//$85582,$85582,,$85043/WR2//"/NC/_N_NC_GD2_ACX/LADAO[37]"/320,170,150/440,170,110/,"#fff4f1"/);测头半径
	DEF GAN_LENGTH=(R/0,//$85676,$85676,,$85043/WR2//"/NC/_N_NC_GD2_ACX/LADAO[239]"/320,190,150/440,190,110/,"#fff4f1"/);测量杆总长
	DEF VAR1=(R/0,//$85571,$85571,,$85043/WR2//"/NC/_N_NC_GD2_ACX/LADAO[22]"/320,210,150/440,210,110/,"#fff4f1"/);安全抬高
	DEF VAR11=(R/0,//$85599,$85599,,$85043/WR2//"/NC/_N_NC_GD2_ACX/LADAO[132]"/320,230,150/440,230,110/,"#fff4f1"/);测量接触反向距离

	DEF VAR10=(R/0,//$85598,$85598,,$85043/WR2//"/NC/_N_NC_GD2_ACX/LADAO[131]"/320,260,150/440,260,110/,"#e1ffe3"/);自动测量前刃相对接触位偏移.z
	DEF VAR8=(R/0,//$85580,$85580,,$85043/WR2//"/NC/_N_NC_GD2_ACX/LADAO[29]"/320,280,150/440,280,110/,"#e1ffe3"/);自动测量底面相对前刃距离Z
	DEF VAR9=(R/0,//$85581,$85581,,$85043/WR2//"/NC/_N_NC_GD2_ACX/LADAO[30]"/320,300,150/440,300,110/,"#e1ffe3"/);自动测量顶面相对前刃距离Z
	DEF LAST_BEI=(R/0,//$85557,$85557,,$85043/WR2//"/NC/_N_NC_GD2_ACX/LADAO[184]"/290,320,200/440,320,110/,"#e1ffe3"/);最后一个齿背底面测量偏置.Z

	DEF AUTO_CHOICE=(I/*0=$85595,1=$85596,2=$85597,3=$85672,4=$85677//$85594,$85594,,/WR2//"/NC/_N_NC_GD2_ACX/LADAO[94]"/320,10,202/440,10,80/,"#ffc5c5");自动测量-测量位置(0全测/1前刃)

	DEF MEAS_1ST_SIDE=(I/*0=$85190,1=$85191//$85679,$85679,,/WR2//"/NC/_N_NC_GD2_ACX/LADAO[466]"/10,140,202/130,140,60/,"#ffc5c5"/);自动对刀-是否测量第一齿齿侧面(0否1是) -- 用来计算通磨起始X
	DEF MEAS_1ST_DIRECTION=(I/*1=$85609,-1=$85610//$85608,$85608,,/WR4//"/NC/_N_NC_GD2_ACX/LADAO[336]"/10,160,202/130,160,60/,"#e1ffe3"/);自动对刀-测量第一齿齿侧面测量方向.X(1正向 -1负向)
	DEF MEAS_1ST_PIANYI_X=(R///$85680,$85680,,/WR4//"/NC/_N_NC_GD2_ACX/LADAO[467]"/10,180,202/130,180,60/,"#e1ffe3"/);测量第一齿齿侧面偏移距离.X
	DEF MEAS_1ST_PIANYI_Y=(R///$85681,$85681,,/WR4//"/NC/_N_NC_GD2_ACX/LADAO[468]"/10,200,202/130,200,60/,"#e1ffe3"/);测量第一齿齿侧面偏移距离.Y
	DEF MEAS_1ST_PIANYI_Z=(R///$85686,$85686,,/WR4//"/NC/_N_NC_GD2_ACX/LADAO[470]"/10,220,202/130,220,60/,"#e1ffe3"/);测量第一齿齿侧面偏移距离.Z
	DEF MEAS_1ST_VALUE=(R///$85682,$85682,,/WR4//"/NC/_N_NC_GD2_ACX/LADAO[469]"/10,240,202/130,240,60/"#5ded67"/);测量第一齿齿侧面测量值.X

	DEF MEAS_FENGDU=(I/*0=$85190,1=$85738,2=$85739//$85697,$85697,,/WR4//"/NC/_N_NC_GD2_ACX/LADAO[324]"/10,160,202/130,160,60/,"#ffc5c5"/);自动测量-是否测量齿轮齿槽精度(0否1是)
	DEF CAO_LENGTH=(R///$85698,$85698,,/WR4//"/NC/_N_NC_GD2_ACX/LADAO[325]"/10,180,202/130,180,60/,"#e1ffe3"/);自动测量-测量齿轮齿槽-齿槽长度
	DEF MEAS_MAX_FENGDU=(R4///$85735,$85735,,/WR4//"/NC/_N_NC_GD2_ACX/LADAO[322]"/10,200,202/130,200,60/"#5ded67"/);自动测量-通磨槽-最大分度误差
	DEF MEAS_MAX_CIXIANG=(R4///$85736,$85736,,/WR4//"/NC/_N_NC_GD2_ACX/LADAO[323]"/10,220,202/130,220,60/"#5ded67"/);自动测量-通磨槽-最大齿向误差
	DEF MEAS_MAX_DIA=(R4///$85737,$85737,,/WR4//"/NC/_N_NC_GD2_ACX/LADAO[334]"/10,200,202/130,200,60/"#5ded67"/);自动测量-通磨槽-最大外径误差

	DEF MEAS_BASE_1=(R///$85691,$85694,,/WR3//"/NC/_N_NC_GD2_ACX/LADAO[476]"/10,280,220/130,280,60/"#5ded67",129/);自动计算磨削位_实际测量-测头相对回转中心虚拟半径
	DEF MEAS_BASE_2=(R///$85692,$85695,,/WR3//"/NC/_N_NC_GD2_ACX/LADAO[477]"/10,300,220/130,300,60/"#5ded67",129/);自动计算磨削位_实际测量-测头虚拟半径和测量杆的夹角
	DEF MEAS_BASE_3=(R///$85693,$85696,,/WR3//"/NC/_N_NC_GD2_ACX/LADAO[478]"/10,320,220/130,320,60/"#5ded67",129/);自动计算磨削位_实际测量-测头测量时虚拟半径垂直向下的夹角

	DEF TYPE=(I////WR4//"/NC/_N_NC_GD2_ACX/GRIND[1]"/0,0,0/0,0,0/);磨削类型
	DEF TECH=(I////WR4//"/NC/_N_NC_GD2_ACX/PROCESS[16]"/0,0,0/0,0,0);精简工艺参数/扩展工艺参数
	DEF PIECE_VOLUME=(I////WR4//"/NC/_N_NC_GD2_ACX/INI[27]"/0,0,0/0,0,0/);单件/批量磨削方式选择
	DEF LOAD_YUANLADAO=(I////WR4//"/NC/_N_NC_GD2_ACX/LADAO[257]"/0,0,0/0,0,0);是否有圆拉刀
	DEF LADAO_CHOICE=(I////WR4//"/NC/_N_NC_GD2_ACX/LADAO[220]"/0,0,0/0,0,0/);拉刀类型(0平面拉刀/1圆拉刀)
	DEF GRIND_CHOICE=(I////WR4//"/NC/_N_NC_GD2_ACX/LADAO[38]"/0,0,0/0,0,0/);加工选择

	HS1=(["\\S_003.png",$85066],ac7,se1,pa0);磨削参数
	HS2=(["\\S_004.png",$85067],ac7,se1,pa0);工艺参数
	HS3=(["\\S_005.png",$85068],ac7,se1,pa0);修整参数
	;
	HS4=(["\\S_006.png",$85069],ac7,se3,pa0);自动对刀 ifIsAuto
	HS5=(["\\S_008.png",$85076],ac7,se1,pa0);加工数据
	HS6=(["\\S_007.png",$85085],ac7,se1,pa0);
	HS7=(["\\S_009.png",$85099],ac7,se1,pa0);磨削基准

	;

	HS8=(["\\S_010.png",$85073],ac7,se1,pa0);返回

	LOAD
		LG("Measure","GRID","a_auto.com")

		IF LOAD_YUANLADAO.VAL==0
			SIGNAL.WR=1
			SIGNAL_1.WR=4
			SIGNAL_2.WR=4
			LISTDELETEITEM("SIGNAL_1",0)
			LISTDELETEITEM("SIGNAL_1",1)
			LISTDELETEITEM("SIGNAL_2",0)
			LISTDELETEITEM("SIGNAL_2",1)

			C_IM.WR=4
			VAR_C.WR=4
			POS_INPUT_7.WR=4
			LISTDELETEITEM("POS_INPUT_7",0)
			LISTDELETEITEM("POS_INPUT_7",1)

		ELSE
			SIGNAL.WR=4
			LISTDELETEITEM("SIGNAL",0)
			LISTDELETEITEM("SIGNAL",1)
			SIGNAL_1.WR=1
			SIGNAL_2.WR=1

			C_IM.WR=1
			VAR_C.WR=2
			POS_INPUT_7.WR=2
		ENDIF

		IF (MEAS_1ST_DIRECTION.VAL<>1) AND (MEAS_1ST_DIRECTION.VAL<>-1)
			MEAS_1ST_DIRECTION.VAL=1
		ENDIF
	END_LOAD

	PRESS(HS1)
		IF TYPE.VAL<>1
			LM("MASK51","a_grind.com")
		ELSE
			LM("MASK18","a_grind.com")
		ENDIF
	END_PRESS

	PRESS(HS2)
		LM("MASK1","a_process.com")
	END_PRESS

	PRESS(HS3)
		LM("MASK3","a_dress.com")
	END_PRESS

	PRESS(HS4)
		LM("MASK21","a_auto.com")
	END_PRESS

	PRESS(HS5)
		LM("MASK1","a_grind_pos.com")
	END_PRESS
	
	PRESS(HS6)
		LM("MASK1","a_auto_pos.com")
	END_PRESS
	
	PRESS(HS7)
		LM("MASK0","a_data.com")
	END_PRESS

	PRESS(HS8)
		EXIT
	END_PRESS

	PRESS(VS1)
		LM("MASK50","a_auto.com")
	END_PRESS
	
	CHANGE(POS_INPUT_1)
		IF POS_INPUT_1.VAL==1
			VAR0.VAL=X_IM.VAL
			POS_INPUT_1.VAL=0
		ENDIF
	END_CHANGE
	
	CHANGE(POS_INPUT_3)
		IF POS_INPUT_3.VAL==1
			VAR2.VAL=Z_IM.VAL
			POS_INPUT_3.VAL=0
		ENDIF
	END_CHANGE
	
	CHANGE(POS_INPUT_4)
		IF POS_INPUT_4.VAL==1
			VAR3.VAL=A_IM.VAL
			POS_INPUT_4.VAL=0
		ENDIF
	END_CHANGE
	
	CHANGE(POS_INPUT_5)
		IF POS_INPUT_5.VAL==1
			VAR4.VAL=B_IM.VAL
			POS_INPUT_5.VAL=0
		ENDIF
	END_CHANGE
	
	CHANGE(POS_INPUT_6)
		IF POS_INPUT_6.VAL==1
			VAR5.VAL=Y_IM.VAL
			POS_INPUT_6.VAL=0
		ENDIF
	END_CHANGE
	
	CHANGE(POS_INPUT_7)
		IF POS_INPUT_7.VAL==1
			VAR_C.VAL=C_IM.VAL
			POS_INPUT_7.VAL=0
		ENDIF
	END_CHANGE
	
	CHANGE(MEAS_1ST_SIDE)
		IF MEAS_1ST_SIDE.VAL==0
			MEAS_1ST_DIRECTION.WR=4
			MEAS_1ST_PIANYI_X.WR=4
			MEAS_1ST_PIANYI_Y.WR=4
			MEAS_1ST_PIANYI_Z.WR=4
			MEAS_1ST_VALUE.WR=4

			IF (LADAO_CHOICE.VAL==0) OR (GRIND_CHOICE<>1);平拉刀或者不是通磨
				MEAS_FENGDU.WR=4
				MEAS_FENGDU.VAL=0
			ELSE
				MEAS_FENGDU.WR=2
			ENDIF
		ELSE
			MEAS_1ST_DIRECTION.WR=2
			MEAS_1ST_PIANYI_X.WR=2
			MEAS_1ST_PIANYI_Y.WR=2
			MEAS_1ST_PIANYI_Z.WR=2
			MEAS_1ST_VALUE.WR=1

			MEAS_FENGDU.WR=4
			MEAS_FENGDU.VAL=0
		ENDIF
		call("UP1")
	END_CHANGE

	CHANGE(MEAS_FENGDU)
		call("UP1")
	END_CHANGE

	SUB(UP1)
		IF MEAS_FENGDU.WR==4
			CAO_LENGTH.WR=4
			MEAS_MAX_FENGDU.WR=4
			MEAS_MAX_CIXIANG.WR=4
			MEAS_MAX_DIA.WR=4
		ELSE
			IF MEAS_FENGDU.VAL==0
				CAO_LENGTH.WR=4
				MEAS_MAX_FENGDU.WR=4
				MEAS_MAX_CIXIANG.WR=4
				MEAS_MAX_DIA.WR=4
			ELSE
				CAO_LENGTH.WR=2
				IF MEAS_FENGDU.VAL==1
					MEAS_MAX_FENGDU.WR=1
					MEAS_MAX_CIXIANG.WR=1
					MEAS_MAX_DIA.WR=4
				ELSE
					MEAS_MAX_FENGDU.WR=4
					MEAS_MAX_CIXIANG.WR=4
					MEAS_MAX_DIA.WR=1
				ENDIF
			ENDIF
		ENDIF
	END_SUB
//END
